###
This is command library

to wipe out Cakefile from realization 
###

async         = require 'async'
path          = require 'path'
fs            = require 'fs-extra'
_             = require 'lodash'

Coffee        = require 'coffee-script'

walk          = require 'walkdir'

# add color to console
module.exports = require './colorizer'

###
Generate array of files from directory, selected on filter as RegExp
###
make_files_list = (in_dir, filter_re, cb) ->
  file for file in walk.sync in_dir when file.match filter_re

###
эта штука дает нам суб-директории, которые будут зеркально создаватся в результате
###
result_dir_suff = (source_dir, source_file) ->
  src_file = path.dirname source_file
  path.relative source_dir, src_file

###
CoffeeScript-to-JavaScript deep builder
###
build_coffee = (source_dir, result_dir, cb) ->
  console.log "Build started for #{source_dir}".yellow
  files = make_files_list source_dir, /\.coffee$/

  compile_file = (file, acb) ->

    this_result_dir = path.join result_dir, result_dir_suff source_dir, file
    this_result_file = path.join this_result_dir, path.basename file, '.coffee'

    fs.readFile file, 'utf8', (err, data) ->
      return acb err if err?
      compiled = "// Generated by CoffeeScript #{Coffee.VERSION}\n"
      compiled += Coffee.compile data
      fs.outputFile "#{this_result_file}.js", compiled, (err) ->
        return acb err if err?
        console.log " -> compiled |#{file}|".grey
        acb()

  async.each files, compile_file, (err) ->
    throw err if err
    console.log ' -> build done'.magenta
    cb() if typeof cb is 'function' 

  null


module.exports = 
  build_coffee        : build_coffee


